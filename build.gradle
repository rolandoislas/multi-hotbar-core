buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: "forge"
apply plugin: "com.matthewprenger.cursegradle"

ext.build = System.getenv("TRAVIS_BUILD_NUMBER")
ext.tag = System.getenv("TRAVIS_TAG")
ext.signKeyPass = System.getenv("SIGN_KEY_PASS")
ext.signKeyStorePass = System.getenv("SIGN_KEY_STORE_PASS")
version = "1.7.10-1.0"
version =  version + "-build-T" + ext.build
group= "com.rolandoislas.multihotbar" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "multihotbarcore"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "run"
    replace("@VERSION@", project.version)
}

task signJar(dependsOn: "jar") {
    onlyIf {
        project.ext.signKeyPass
        project.ext.signKeyStorePass
        project.ext.tag
    }
    doLast {
        ant.signjar(
                destDir: jar.archivePath.parent,
                jar: jar.archivePath,
                alias: "multihotbarcore",
                storetype: "jks",
                keystore: ".travis/multihotbar.jks",
                storepass: project.ext.signKeyStorePass,
                keypass: project.ext.signKeyPass,
                verbose: true,
                preservelastmodified: true
        )
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'com.rolandoislas.multihotbar.asm.FmlLoadingPlugin'
    }
}

build {
    dependsOn signJar
}

if (ext.tag) {
    if (ext.tag.contains("release"))
        ext.releaseType = "release"
    else if (ext.tag.contains("beta"))
        ext.releaseType = "beta"
    else
        ext.releaseType = "alpha"
    curseforge {
        apiKey = System.getenv("CURSE_API_KEY") ?: ""
        ext.tag = System.getenv("TRAVIS_TAG") ?: ""
        project {
            id = "280150"
            changelog = System.getenv("TRAVIS_COMMIT_MESSAGE") ?: ""
            releaseType = project.releaseType
            mainArtifact jar
        }
    }
}

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

idea.module.inheritOutputDirs = true

dependencies {

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}